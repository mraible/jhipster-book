buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.36.0'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.6.1'
        classpath 'org.asciidoctor:asciidoctorj-epub3:1.5.0-alpha.19'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.4'
        classpath 'org.jruby:jruby-complete:9.2.16.0'
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:1.7.0'
    }
}

plugins {
    id 'de.undercouch.download' version '4.1.1'
    id 'se.patrikerdes.use-latest-versions' version '0.2.15'
    id 'com.github.ben-manes.versions' version '0.38.0'
}

apply plugin: 'java'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.github.jruby-gradle.base'

dependencies {
    gems 'rubygems:coderay:1.1.3'
}

version = '5.0.3'

asciidoctorj {
    version = '2.4.2'
}

import org.asciidoctor.gradle.AsciidoctorTask

def attrs = ['sourcedir'         : '../../../main/webapp',
             'epub3-stylesdir'   : './styles/epub',
             // NOTE don't include leading ./ because it messes up paths in the epub files
             'imagesdir'         : 'images',
             'toc'               : 'left',
             'icons'             : 'font',
             'sectanchors'       : '',
             'idprefix'          : '',
             'idseparator'       : '-',
             'pdf-fontsdir'      : "$projectDir/src/docs/asciidoc/styles/pdf/fonts;GEM_FONTS_DIR;uri:classloader:GEM_FONTS_DIR".toString(),
             'pdf-stylesdir'     : "$projectDir/src/docs/asciidoc/styles/pdf".toString(),
             'listing-caption'   : 'Listing']

tasks.withType(AsciidoctorTask) { task ->
    attributes attrs
    sources {
        include 'index.adoc'
    }
}

task html(type: AsciidoctorTask, description: 'Generates single page HTML') {
    backends 'html5'
}

task prepress(type: AsciidoctorTask, description: 'Generates PDF for prepress printing') {
    attributes attrs + ['media' : 'prepress', 'pdfmarks': '', 'pdf-theme': 'prepress']
    requires file('src/main/ruby/asciidoctor-pdf-extensions.rb')
    backends 'pdf'
    outputDir "$buildDir/asciidoc/pdf-prepress"
    separateOutputDirs false
}

task pdf(type: AsciidoctorTask, description: 'Generates PDF for download') {
    attributes attrs + ['pdfmarks': '', 'source-highlighter': 'coderay']
    requires file('src/main/ruby/asciidoctor-pdf-extensions.rb')
    outputDir "$buildDir/asciidoc/pdf-screen"
    backends 'pdf'
    separateOutputDirs false
}

task epub(type: AsciidoctorTask, description: 'Generates EPUB3') {
    backends 'epub3'
}

pdf.shouldRunAfter html
prepress.shouldRunAfter pdf
epub.shouldRunAfter prepress

task all(dependsOn: ['html', 'pdf', 'prepress', 'epub'])

defaultTasks 'all'
